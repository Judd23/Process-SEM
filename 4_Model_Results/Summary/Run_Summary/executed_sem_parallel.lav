Belong =~ sbvalued + sbmyself + sbcommunity
Gains  =~ pganalyze + pgthink + pgwork + pgvalues + pgprobsolve
SupportEnv =~ SEacademic + SEwellness + SEnonacad + SEactivities + SEdiverse
Satisf =~ sameinst + evalexp
DevAdj =~ 1*Belong + Gains + SupportEnv + Satisf
EmoDiss =~ 1*MHWdacad + MHWdlonely + MHWdmental + MHWdexhaust + MHWdsleep + MHWdfinancial
QualEngag =~ 1*QIadmin + QIstudent + QIadvisor + QIfaculty + QIstaff

# structural (treatment/control + moderation)
# X = x_FASt (0/1), XZ_c = x_FASt*credit_dose_c (treated-only dose effect)
# NOTE: credit_dose_c main effect OMITTED - dose only matters for FASt students
EmoDiss ~ a1*x_FASt + a1z*XZ_c + g1*cohort +
     hgrades_c + bparented_c + pell + hapcl + hprecalc13 + hchallenge_c + cSFcareer_c

QualEngag ~ a2*x_FASt + a2z*XZ_c + g2*cohort +
     hgrades_c + bparented_c + pell + hapcl + hprecalc13 + hchallenge_c + cSFcareer_c

DevAdj ~ c*x_FASt + cz*XZ_c + b1*EmoDiss + b2*QualEngag + g3*cohort +
         hgrades_c + bparented_c + pell + hapcl + hprecalc13 + hchallenge_c + cSFcareer_c

# conditional direct effects of FASt on DevAdj at Z values
dir_z_low  := c + cz*-1.30278493088
dir_z_mid  := c + cz*0
dir_z_high := c + cz*1.30278493088

# conditional a-paths at Z values (FASt -> mediator)
a1_z_low  := a1 + a1z*-1.30278493088
a1_z_mid  := a1 + a1z*0
a1_z_high := a1 + a1z*1.30278493088
a2_z_low  := a2 + a2z*-1.30278493088
a2_z_mid  := a2 + a2z*0
a2_z_high := a2 + a2z*1.30278493088

# conditional indirect effects at Z values
ind_EmoDiss_z_low  := a1_z_low*b1
ind_EmoDiss_z_mid  := a1_z_mid*b1
ind_EmoDiss_z_high := a1_z_high*b1
ind_QualEngag_z_low  := a2_z_low*b2
ind_QualEngag_z_mid  := a2_z_mid*b2
ind_QualEngag_z_high := a2_z_high*b2

# total effects of FASt on DevAdj at Z values
total_z_low  := dir_z_low  + ind_EmoDiss_z_low  + ind_QualEngag_z_low
total_z_mid  := dir_z_mid  + ind_EmoDiss_z_mid  + ind_QualEngag_z_mid
total_z_high := dir_z_high + ind_EmoDiss_z_high + ind_QualEngag_z_high

# indices of moderated mediation (first-stage moderation)
index_MM_EmoDiss   := a1z*b1
index_MM_QualEngag := a2z*b2

